[Documentation](../../README.md) > [Programming](../README.md) > [Tutorials](../Tutorials.md)

## Tutorial 2 : Basic Output

### Hello, world!

In the previous tutorial, we saw how to create the minimum viable product application. Too minimal, in fact. The canonical "first program" for any given language usually displays the text "Hello, world!". Our MC64K processor is virtual and has no hardware. In order to talk to the outside world, we're going to need some help from the host.

Edit the existing main.s

```asm
; Hello world

    @export main x
    @export exit x

main:
    lea     .hello_string, a0
    move.b  #2, -(sp)
    hcf     #0
    add.q   #1, sp

exit:
    rts

.hello_string:
    dc.b "Hello, world!\n\0"
```

Assemble and run. If all has gone to plan:

```shell
:~/tutorial$ ~/MC64000/assembler/run64k tutorial.64x 2>/dev/null
Hello, world!
```

To break down what is going on:

* We load the address of the message into a0.
    - We define the string using the dc.b (declare constant bytes) pseudo operation.
    - This normally expects a comma separated list of integer values but accepts character strings as a shorthand notation.
    - Note that the string literal includes a terminating null character. This isn't C. Forgetting it will end in disaster.
* We push a byte containing the value 2 onto the stack:
    - This byte designates a specific host native operation we want to perform.
* We invoke HCF Vector 0:
    - This designates the host vector for IO operations.
    - The code exits the interpreter here and invokes the specific operation.
    - For HCF Vector 0, operation 2 prints a string pointed to by a0 to the standard output stream of the host.
* We restore the stack.
    - This is important, even though we are about to exit the code.
* Output happens. Magic.

### Improving readability

The use of magic numbers for host vectors and operations is perplexing. Fortunately, we can create definitions for these using the `@def` directive:

```asm
; Hello world

    @export main x
    @export exit x

    @def IO_VECTOR    #0
    @def PRINT_STRING #2

main:
    lea     .hello_string, a0
    move.b  PRINT_STRING, -(sp)
    hcf     IO_VECTOR
    add.q   #1, sp

exit:
    rts

.hello_string:
    dc.b "Hello, world!\n\0"
```
---
**Note:**
* `@def` operates as a search and replace mechanism. Search and replace is carried out in descending order of search term length.
* Constants declared using `@def` have file scope.
* To declare a global, use `@equ`. This will be visible to all files assembled after the point of definition.

---

### Summary

While this is definitely an improvement, it will be tedious and error prone to have to do this all the time.

[<< Prev](./p_01.md) | [Next>>](p_03.md)
